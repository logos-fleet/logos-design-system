import QtQuick
import QtTest

import Logos.Theme
import Logos.Controls

TestCase {
    id: root
    name: "LogosTextField"
    width: 400
    height: 200
    when: windowShown

    LogosTextField {
        id: field
        placeholderText: "Enter text"
        anchors.centerIn: parent
        width: 200
    }

    IntValidator {
        id: rangeValidator
        bottom: 10
        top: 100
    }

    function init() {
        field.text = ""
        field.placeholderText = "Enter text"
        field.echoMode = TextInput.Normal
        field.validator = null
        field.readOnly = false
        // QtTest runs alphabetically; take focus back so a focus test does not
        // leak a focused border into later ones.
        root.forceActiveFocus()
    }

    function test_text_alias_get_set() {
        field.text = "hello"
        compare(field.text, "hello")
        compare(field.textInput.text, "hello")
    }

    function test_input_empty_when_text_cleared() {
        field.text = ""
        compare(field.textInput.text, "")
        compare(field.textInput.text.length, 0)
    }

    function test_input_holds_value_when_text_set() {
        field.text = "x"
        compare(field.textInput.text, "x")
        compare(field.textInput.text.length, 1)
    }

    function test_echo_mode_propagates_to_text_input() {
        field.echoMode = TextInput.Password
        compare(field.textInput.echoMode, TextInput.Password)
    }

    function test_normal_text_color_without_validator() {
        tryCompare(field.textInput, "color", Theme.palette.text)
    }

    function test_normal_border_when_empty_with_validator() {
        field.validator = rangeValidator
        field.text = ""
        tryCompare(field.backgroundItem.border, "color", Theme.palette.backgroundElevated)
    }

    function test_error_border_when_input_not_acceptable() {
        field.validator = rangeValidator
        field.text = "5"
        tryCompare(field.backgroundItem.border, "color", Theme.palette.error)
    }
    function test_error_text_color_when_input_not_acceptable() {
        field.validator = rangeValidator
        field.text = "5"
        tryCompare(field.textInput, "color", Theme.palette.error)
    }

    function test_normal_text_color_when_input_acceptable() {
        field.validator = rangeValidator
        field.text = "50"
        tryCompare(field.textInput, "color", Theme.palette.text)
    }

    function test_read_only_propagates_to_text_input() {
        field.readOnly = true
        compare(field.textInput.readOnly, true)
    }

    function test_focus_reaches_the_editor() {
        field.forceActiveFocus()
        tryCompare(field.textInput, "activeFocus", true)
    }

    // WHAT ANYTHING THAT IS NOT LOOKING AT THE FIELD CALLS IT. The editor is
    // the item the accessibility tree carries -- the Control around it is not
    // in the tree -- and a bare TextInput goes in with an empty name, so a
    // screen reader reads "text field" and a driver has nothing to ask for.
    // The placeholder is the words a sighted user identifies the field by, so
    // it is the name (logos-workspace#174).
    function test_editor_is_named_by_its_placeholder() {
        field.placeholderText = "Account label"
        tryCompare(field.textInput.Accessible, "name", "Account label")
    }

    function test_editor_name_follows_the_placeholder() {
        field.placeholderText = "Seed phrase"
        tryCompare(field.textInput.Accessible, "name", "Seed phrase")
        field.placeholderText = "Chain ID"
        tryCompare(field.textInput.Accessible, "name", "Chain ID")
    }

    function test_joins_tab_focus_chain() {
        compare(field.activeFocusOnTab, true)
        compare(field.focusPolicy, Qt.StrongFocus)
        compare(field.textInput.activeFocusOnTab, true)
    }
}
